buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
    }
    repositories {
        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    compileJava.options.encoding = 'UTF-8'

    group = "com.workshop"
    version = '0.1.0'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        manifest.attributes provider: 'gradle'
    }
}

project(':domain') {
    bootJar {
        enabled = false
    }
}

project(':app') {
    apply plugin: 'com.palantir.docker'

    docker {
        name "${project.group}/${jar.baseName}"
        dockerfile file('../Dockerfile')
        files jar.archivePath
        buildArgs(['JAR_FILE': "${jar.archiveName}"])
    }
}
